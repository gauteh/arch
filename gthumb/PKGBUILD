# $Id: PKGBUILD 79717 2010-05-06 12:23:55Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Gaute Hope <eg@gaute.vetsj.com>
 
pkgname=gthumb
pkgver=2.11.91
pkgrel=1
pkgdesc="Image browser and viewer for the GNOME Desktop"
arch=(i686 x86_64)
license=('GPL')
url="http://gthumb.sourceforge.net/"
depends=('libgphoto2>=2.4.9' 'libgnomeui>=2.24.3' 'libxtst>=1.1.0' 'libxxf86vm>=1.1.0' 'desktop-file-utils')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.1')
options=('!libtool' '!emptydirs')
install=gthumb.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('c986a210bdc4127c97b36fa2b4f20efd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gthumb ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
