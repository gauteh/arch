# $Id: PKGBUILD 41460 2011-03-06 17:46:31Z jelle $
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Jonathan Liu <net147@hotmail.com>
# Contributor: Gaute Hope <eg@gaute.vetsj.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=awn-extras-applets
pkgver=0.4.0
pkgrel=8
pkgdesc="A collection of applets for avant-window-navigator"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn-extras"
license=('GPL2')
depends=('avant-window-navigator' 'gnome-menus' 'libgtop' 'libnotify' 
         'libsexy' 'vte' 'gstreamer0.10-python')
makedepends=('intltool' 'libdesktop-agnostic' 'vala')
optdepends=('python-notify: needed for some applets'
            'python-vobject: needed for some applets')
provides=('avant-window-navigator-extras')
install=awn-extras-applets.install
options=('!libtool')
source=(http://launchpad.net/awn-extras/0.4/0.4.0/+download/awn-extras-0.4.0.tar.gz  \
    awn-extras-libnotify0.7.patch
    fixswa.patch)
md5sums=('b559d68cd6ad295c961c20fcc5d9f9c0'
         '7999955956d1d2457b4c3b50ffd3199d'
         '614cf5402206fe0aa4a97bbc67b57134')

build() {
  cd ${srcdir}/${pkgname/-applets/}-${pkgver}
  patch -Np1 -i $srcdir/awn-extras-libnotify0.7.patch
  patch -Np1 -i $srcdir/fixswa.patch
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-pymod-checks
  make
}

package() {
  cd ${srcdir}/${pkgname/-applets/}-${pkgver}
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  # yes... theses guys can not even get a "#!" right
  sed -i -e "s|#[ ]*[!]*[ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
         -e "s|#[ ]*![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    $(find $pkgdir/usr/share/avant-window-navigator/applets/ -name '*.py') \
    $pkgdir/usr/lib/python2.7/site-packages/awn/extras/awnmediaplayers.py

  mkdir -p ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/awn-extras.schemas \
    ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
  rmdir --ignore-fail-on-non-empty -p ${pkgdir}/usr/share/locale
  rmdir --ignore-fail-on-non-empty -p ${pkgdir}/etc/gconf/schemas
}
# vim:set ts=2 sw=2 et:
