# Contributor: Gaute Hope <eg@gaute.vetsj.com>

# This package installs the source aswell as SUP/ archives

pkgname=seisan
pkgver=9.1
prevfull=9.0.1
pkgrel=1
pkgdesc="SEISAN Earthquake analysis software package"
arch=('i386 x86_64')
url="http://www.uib.no/rg/geodyn/artikler/2010/02/software"
license=('custom')
depends=('gcc-fortran')
source=("seisan${pkgver/./}.tar.gz"
        "ftp://ftp.geo.uib.no/pub/seismo/SOFTWARE/SEISAN_${prevfull}/seisan_training_data.tar.gz"
        "ftp://ftp.geo.uib.no/pub/seismo/SOFTWARE/SEISAN_${prevfull}/seisan_v${prevfull}_linux_64bit.tar.gz"
        )
noextract=("seisan_v${prevfull}_linux_64bit.tar.gz")

build() {
  cd ${srcdir}

  mkdir prevfull/
  tar -C prevfull -zxf "seisan_v${prevfull}_linux_64bit.tar.gz"

  # Rebuilding..
  export SEISARCH=linux64
  export OUTPATH=${pkgdir}

  cd LIB/
  rm -rf libmseed
  make clean
  tar zxf ../prevfull/SUP/libmseed-*.tar.gz

  cd ../PRO
  make clean
  make all

  # Regenerate IASP91 model
  echo "-----------------------------------------"
  echo "==> Regenerating IASP91 model.."
  cd ../WOR/
  ../PRO/${SEISARCH}/remodl > /dev/null 2>&1
  ../PRO/${SEISARCH}/setbrn > /dev/null 2>&1
  mv IASP* ../DAT/
  rm remodl*
  rm setbrn*

  cd ../
}

package () {
  mkdir -p ${pkgdir}/opt/seisan
  mkdir ${pkgdir}/opt/seisan/{LIB,PRO}

  cp -r "${srcdir}/PRO/${SEISARCH}"/* ${pkgdir}/opt/seisan/PRO/
  cp -r "${srcdir}/LIB/${SEISARCH}"/* ${pkgdir}/opt/seisan/LIB/

  cp -r ${srcdir}/{CAL,COM,DAT,INC,INF,ISO,PIC,REA,SUP,TMP,WAV,WOR} ${pkgdir}/opt/seisan

  # Add seisan dirs to PATH
  mkdir -p ${pkgdir}/etc/profile.d
  echo 'export PATH=$PATH:/opt/seisan/PRO' > ${pkgdir}/etc/profile.d/seisan.sh
  chmod +x ${pkgdir}/etc/profile.d/seisan.sh
}

md5sums=('2db3d2faef01a9f3238fa6060fe25d5d'
         'e4f81a1d85d8259c80cb8a0bd839168a'
         '77bd256da404a078c9bd25e686c9792a')
