# Maintainer: Gaute Hope <eg@gaute.vetsj.com>

pkgname=seisan
pkgver=9.1.1_pre
pkgrel=3
pkgdesc="SEISAN Earthquake analysis software package"
arch=('x86_64')
url="http://www.uib.no/rg/geodyn/artikler/2010/02/software"
license=('custom')
makedepends=('gcc-fortran' 'gcc')
source=("ftp://ftp.geo.uib.no/pub/seismo/SOFTWARE/SEISAN/SEISAN_9.1.1/seisan_v9.1.1_linux_source_pre.tar.gz"
        "ftp://ftp.geo.uib.no/pub/seismo/SOFTWARE/SEISAN/seisan_test_data.tar.gz"
        )

noextract=("seisan_test_data.tar.gz")

build() {
  cd ${srcdir}

  # Rebuilding.. (for x86_64, might work for i386 as well)
  export SEISARCH=linux64
  export OUTPATH=${pkgdir}

  cd LIB/
  make clean

  cd ../PRO/

  make clean
  make all

  # Regenerate IASP91 model
  echo "-----------------------------------------"
  echo "==> Regenerating IASP91 model.."
  cd ../WOR/
  ../PRO/${SEISARCH}/remodl > /dev/null 2>&1
  ../PRO/${SEISARCH}/setbrn > /dev/null 2>&1
  mv IASP* ../DAT/
  rm remodl*
  rm setbrn*

  cd ../

  # Increase MULPLT default resolution (screen & hard copy)
  echo "==> Increasing MULPLT default resolution to 60000 points (screen and h/c).."
  sed -i -e 's|points pl. screen  1500.0|points pl. screen  60000|' DAT/MULPLT.DEF
  sed -i -e 's|points pl. hc      3000.0|points pl. hc      60000|' DAT/MULPLT.DEF

  # Update seisan home path
  echo "==> Update SEISAN home path to /opt/seisan.."
  sed -i -e 's|/home/seisan|/opt/seisan|' COM/SEISAN.bash
  sed -i -e 's|/home/s2000/seismo|/opt/seisan|' COM/SEISAN.csh

  # Change default filters in MULPLT
  #echo "==> Warning: Changing default filters in MULPLT.."
  #sed -i -e 's|FILTER 1(.*)|FILTER 1                                8.0       25.0 |' DAT/MULPLT.DEF
  #sed -i -e 's|FILTER 2(.*)|FILTER 2                                20.0      50.0 |' DAT/MULPLT.DEF
}

package () {
  mkdir -p ${pkgdir}/opt/seisan
  mkdir ${pkgdir}/opt/seisan/{LIB,PRO}

  cp -r "${srcdir}/PRO/${SEISARCH}"/* ${pkgdir}/opt/seisan/PRO/
  cp -r "${srcdir}/LIB/${SEISARCH}"/* ${pkgdir}/opt/seisan/LIB/

  # copy pre-compiled msrepack and friends
  cp "${srcdir}/PRO/"{gmtxy,msrepack,rdseed,seisan2mseed} "${pkgdir}/opt/seisan/PRO/"

  cp -r ${srcdir}/{CAL,COM,DAT,INC,INF,ISO,PIC,REA,SUP,TMP,WAV,WOR} ${pkgdir}/opt/seisan

  # Install training data
  cp "${srcdir}/seisan_test_data.tar.gz" "${pkgdir}/opt/seisan/INF/"
  echo "==> Test data is located in /opt/seisan/INF."

  # Add seisan dirs to PATH
  mkdir -p ${pkgdir}/etc/profile.d
  echo "export PATH=\$PATH:/opt/seisan/PRO" > ${pkgdir}/etc/profile.d/seisan.sh
  chmod +x ${pkgdir}/etc/profile.d/seisan.sh
}

md5sums=('1b4b2eeac6b0704cf50cbe8570624764'
         '17a83cb87732ced23f066712773be29d')

